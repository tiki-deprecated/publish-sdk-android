buildscript {
    ext.flutterEngine = "1.0.0-c08d7d5efc9aa6eb3c30cfb3be6dc09bca5e7631"
    ext.sdkVersion = "1.1.2"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.kezong:fat-aar:1.3.8'
    }
}

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 33
    defaultConfig {
        minSdk 21
        versionCode 4
        versionName "1.1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lint {
        baseline = file("lint-baseline.xml")
    }
}

apply plugin: 'com.kezong.fat-aar'
apply plugin: 'kotlin-kapt'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "com.squareup.moshi:moshi-kotlin:1.14.0"
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.14.0'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    testImplementation 'org.mockito:mockito-core:4.11.0'
    androidTestImplementation 'org.mockito:mockito-android:4.11.0'

    debugEmbed "com.squareup.moshi:moshi-kotlin:1.14.0"
    releaseEmbed "com.squareup.moshi:moshi-kotlin:1.14.0"

    debugEmbed "com.squareup.moshi:moshi:1.14.0"
    releaseEmbed "com.squareup.moshi:moshi:1.14.0"

    debugEmbed "com.squareup.okio:okio:2.10.0"
    releaseEmbed "com.squareup.okio:okio:2.10.0"

    debugEmbed "io.flutter:flutter_embedding_debug:$flutterEngine"
    releaseEmbed "io.flutter:flutter_embedding_release:$flutterEngine"

    debugEmbed "com.mytiki.tiki_sdk_flutter:flutter_debug:$sdkVersion"
    releaseEmbed "com.mytiki.tiki_sdk_flutter:flutter_release:$sdkVersion"

    debugEmbed "io.flutter.plugins.pathprovider:path_provider_android_debug:$sdkVersion"
    releaseEmbed "io.flutter.plugins.pathprovider:path_provider_android_release:$sdkVersion"

    debugEmbed "com.it_nomads.fluttersecurestorage:flutter_secure_storage_debug:$sdkVersion"
    releaseEmbed "com.it_nomads.fluttersecurestorage:flutter_secure_storage_release:$sdkVersion"

    debugEmbed "eu.simonbinder.sqlite3_flutter_libs:sqlite3_flutter_libs_debug:$sdkVersion"
    releaseEmbed "eu.simonbinder.sqlite3_flutter_libs:sqlite3_flutter_libs_release:$sdkVersion"
}

apply plugin: "org.jetbrains.dokka"
tasks.dokkaHtml.configure {
    outputDirectory.set(file("../doc/api"))
}

def groovyShell = new GroovyShell()
def addDll = groovyShell.parse(new File("$rootDir/app/add-dll.gradle"))

addDll.clean(rootDir.toString())
addDll.flutter(rootDir.toString(), flutterEngine)
addDll.sqlite(rootDir.toString())

apply plugin: 'maven-publish'
apply plugin: 'signing'

signing {
    def signingKey = System.getenv("PGP_PRIVATE_KEY")
    def signingPassword = System.getenv("PGP_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.mytiki'
                artifactId = 'tiki-sdk-android'
                version = android.getDefaultConfig().getVersionName()
                pom {
                    name = 'TIKI SDK [Android]'
                    description = "A package for adding TIKI's decentralized infrastructure to Android projects. Add tokenized data ownership, consent, and rewards to your app in minutes."
                    url = 'https://docs.mytiki.com/docs/tiki-sdk-android-getting-started'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/tiki/tiki-sdk-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            name = 'The TIKI Team'
                            email = 'hello@mytiki.com'
                            organization = 'TIKI'
                            organizationUrl = 'https://mytiki.com'
                        }
                    }
                    scm {
                        url = 'https://github.com/tiki/tiki-sdk-android'
                        tag = android.getDefaultConfig().getVersionName()
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'localRepo'
                url = layout.buildDirectory.dir("repo")
            }
            maven {
                name = "OSSRH"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv('OSSRH_USER')
                    password = System.getenv('OSSRH_TOKEN')
                }
            }
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/tiki/tiki-sdk-android"
                credentials {
                    username = System.getenv("GITHUB_USER")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}